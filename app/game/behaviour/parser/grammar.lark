?start: _NL* composite

composite: COMPOSITE_TAG _NL _INDENT _child_node+ _DEDENT
decorator: "@" NODE_IDENTIFIER [_NL] _child_node
node: NODE_IDENTIFIER [arguments] [input_memory] [output_memory] [COMMENT] _NL+
_child_node: decorator | composite | node
arguments: IDENTIFIER+ [OPERATOR OPERAND]
         | NUMBER+
input_memory: "<<" [SELF] IDENTIFIER ("." IDENTIFIER)*
output_memory: ">>" IDENTIFIER ("." IDENTIFIER)*

COMPOSITE_TAG: "-?-" | "-->"
NODE_IDENTIFIER: /[a-z]+(-[a-z]+)*/
OPERATOR: "<=" | ">=" | "==" | "is" | "!=" | "not" | "<" | ">"
OPERAND: "true" | "false" | NUM_OR_PERCENT
NUM_OR_PERCENT: NUMBER ["%"]
SELF: "."
IDENTIFIER: /[\w_]+/
COMMENT: "#" /[^\n]/*
_NL: /(\r?\n[\t ]*)+/

%import common.NUMBER
%import common.WS_INLINE
%ignore WS_INLINE
%declare _INDENT _DEDENT
